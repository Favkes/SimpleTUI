plugins {
    id 'application'
    id 'java-library'
    id 'maven-publish'
}

group = 'com.github.favkes'

// Dynamic version based on tag
def gitRef = System.getenv("GITHUB_REF") ?: "refs/heads/master"
version = gitRef.startsWith("refs/tags/") ?
        gitRef.replace("refs/tags/v", "") :
        "1.0.0"   // fallback must be valid string

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.fusesource.jansi:jansi:2.4.0'
    implementation 'org.jline:jline:3.26.0'
}

java {
    withSourcesJar()
    withJavadocJar()
}

application {
    mainClass = 'Main'
}

publishing {
    publications {
        create("mavenJava", MavenPublication) {
            groupId = project.group
            artifactId = "simpletui"
            version = project.version
            from components.java

            pom {
                name.set("SimpleTUI")
                description.set("A simple yet effective terminal UI library in Java")
                url.set("https://github.com/Favkes/SimpleTUI")
                licenses {
                    license {
                        name.set("MIT License")
                        url.set("https://opensource.org/licenses/MIT")
                    }
                }
                developers {
                    developer {
                        id.set("Favkes")
                        name.set("Favkes ;)")
                        email.set("contact@favkes.com")
                    }
                }
                scm {
                    connection.set("scm:git:git://github.com/Favkes/SimpleTUI.git")
                    developerConnection.set("scm:git:ssh://github.com/Favkes/SimpleTUI.git")
                    url.set("https://github.com/Favkes/SimpleTUI")
                }
            }
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/Favkes/SimpleTUI")
            credentials {
                username = System.getenv("GITHUB_ACTOR")  // automatic actor
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}

// debug task
task printVersion {
    doLast {
        println ">>> Gradle project version: ${project.version}"
    }
}