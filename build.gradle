plugins {
    id 'application'
    id 'java-library'
    id 'maven-publish'
}

group = 'com.github.favkes.simpletui'
def gitRef = System.getenv("GITHUB_REF_NAME") ?: "refs/heads/master"
version = gitRef.startsWith("refs/tags/") ?
        gitRef.replace("refs/tags/v", "") :
        "1.0.0-SNAPSHOT"

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.fusesource.jansi:jansi:2.4.0'
    implementation 'org.jline:jline:3.26.0'
}

java {
    withSourcesJar()    // generate sources jar
    withJavadocJar()    // generate javadoc jar
}

application {
    mainClass = 'Main' // replace with your fully qualified Main class
}

publishing {
    publications {
        create("mavenJava", MavenPublication) {
            groupId = project.group
            artifactId = "SimpleTUI"    // (== repo name)
            version = project.version
            from components.java
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/Favkes/SimpleTUI")   // local publication
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_USERNAME")
                password = project.findProperty("gpr.token") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }
}

tasks.withType(PublishToMavenRepository) {
    onlyIf {
        // only run when pushed with a tag (release)
        System.getenv("GITHUB_REF_NAME")?.startsWith("refs/tags/") ?: false
    }
}